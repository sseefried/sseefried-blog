% Proving a more general theorem is often easier
% Sean Seefried
% Fri 18 Feb 2022

## Introduction

A common approach I follow when programming -- and I'm not proving the correctness of those programs -- is to "generalise from the specific". While trying to solve a programming problem, I find it easier to work with more concrete concepts first and only later to generalise my solution.

A simple of example of this might be that I'll write a function to sort a list of integers before going on to generalise it to sort a list of anything that can be ordered.

But I've discovered something counter-intuitive. When proof is involved, this can actually be harder. Proofs on specific structures can actually be a lot harder than proofs on more general structures. If this is true in general then it makes sense to do the hard work of generalising one's programs up-front because it will making proving them correct _easier_. Further I suspect the combined work of a) writing the program and proving it correct will be less than b) writing a specific program, trying to prove it, failing, generalising the program and finally proving it correct.

Even thought this seems completely counter-intuitive I suspect it's just a consequence of the fact that general structures, lacking concreteness, are _defined by_ their mathematical properties and these mathematical properties are useful in proving correctness.

In this post I'd like to show you an example of a specific program that was hard to prove correct but became much easier once I had generalised the program.

## Bijections that permute a finite set

I defined a _permutation_ as a bijection from `Fin n` to `Fin n` for some `n`. For convenience I renamed `Fin` to `ğ”½` because I really value conciseness and it has echoes of `Data.Nat`'s `â„•`.

```agda
Perm : â„• â†’ Set
Perm n = ğ”½ n â†” ğ”½ n
```

In Agda a bijection is constructed from:

- a function going in the forward direction: `f`
- a function going in the backwards direction: `fâ»Â¹`
- a proof that `fâ»Â¹ âˆ˜ f â‰¡ id` i.e. that `fâ»Â¹` is a left-inverse of `f`
- a proof that `f âˆ˜ fâ»Â¹ â‰¡ id` i.e. that `fâ»Â¹` is a right-inverse of `f`

## Introducing `+1-mod-n` and `-1-mod-n`

I thought I'd play around with a very simple permutation that, in the forward direction, evaluates to the successor of its input modulo `n`.

```agda
+1-mod-n : {n : â„•} â†’ ğ”½ n â†’ ğ”½ n
+1-mod-n {â„•.suc n} m with n â„•.â‰Ÿ toâ„• m
... | no mâ‰¢n  = suc (lowerâ‚ m mâ‰¢n)
... | yes _ = zero
```

And here is its inverse:

```agda
-1-mod-n : {n : â„•} â†’ ğ”½ n â†’ ğ”½ n
-1-mod-n {â„•.suc n} zero = fromâ„• n
-1-mod-n {â„•.suc n} (suc m) = injectâ‚ m
```

I liked the definition for `-1-mod-n` much better than the `+1-mod-n`, but couldn't think of an alternative way to define the latter. I then got stuck into trying to prove that `-1-mod-n` is the left-inverse of `+1-mod-n`.

The proof took me many hours but eventually I came up with the following. It's a very ugly proof and the only reason I have posted it here is so that you can have an "ugh"-reaction.

```agda
iâ‰¡jâ‡’j<i+1 : âˆ€ {i j } â†’ i â‰¡ j â†’ j â„•.< â„•.suc i
iâ‰¡jâ‡’j<i+1 {i} {j} iâ‰¡j =
  begin
    â„•.suc j
  â‰¡âŸ¨ cong â„•.suc (â‰¡-sym iâ‰¡j) âŸ©
    â„•.suc i
  âˆ
  where
    open Relation.Binary.PropositionalEquality renaming (sym to â‰¡-sym)
    open â„•.â‰¤-Reasoning

open â‰¡-Reasoning

left-inverseâ€² : (n : â„•) â†’ (x : ğ”½ n) â†’ -1-mod-n (+1-mod-n x) â‰¡ x
left-inverseâ€² â„•.zero ()
left-inverseâ€² (â„•.suc â„•.zero) zero = refl
left-inverseâ€² (â„•.suc (â„•.suc nâ€²)) x
            with â„•.suc nâ€² â„•.â‰Ÿ toâ„• x
...  | no n+1â‰¢x with x
...               | zero = refl
...               | suc x =
    begin
      -1-mod-n (suc (lowerâ‚ (suc x) n+1â‰¢x))
    â‰¡âŸ¨âŸ©
      injectâ‚ (lowerâ‚ (suc x) n+1â‰¢x)
    â‰¡âŸ¨  injectâ‚-lowerâ‚ (suc x) n+1â‰¢x âŸ©
      suc x
    âˆ
left-inverseâ€² (â„•.suc (â„•.suc n)) x
     | yes n+1â‰¡x =
   begin
     -1-mod-n zero
   â‰¡âŸ¨âŸ©
     fromâ„• (â„•.suc n)
   â‰¡âŸ¨ fromâ„•-def (â„•.suc n) âŸ©
     fromâ„•< n+1<n+2
   â‰¡âŸ¨ fromâ„•<-cong (â„•.suc n) (toâ„• x) n+1â‰¡x n+1<n+2 (iâ‰¡jâ‡’j<i+1 n+1â‰¡x) âŸ©
      fromâ„•< (iâ‰¡jâ‡’j<i+1 n+1â‰¡x )
   â‰¡âŸ¨ fromâ„•<-toâ„• x (iâ‰¡jâ‡’j<i+1 n+1â‰¡x)  âŸ©
     x
   âˆ
   where
     n+1<n+2 : â„•.suc n â„•.< â„•.suc (â„•.suc n)
     n+1<n+2 = â„•.sâ‰¤s (â„•.sâ‰¤s (â„•.â‰¤-reflexive refl))
```

I also tried to prove that `-1-mod-n` was the right inverse of `+1-mod-n` but got stuck almost straight away and gave up.

## A generalisation of the pattern of permutation

If you look carefully there is an obvious generalisation of the pattern of permutation staring us in the face. Imagine all the possible values of `Fin n` written down, in order, as a list:

```
[ 0, 1, ..., n - 1 ]
```

Now imagine _splitting_ this list into two sub-lists:

```
[ 0, 1, ... n - 2 ] [ n - 1]
```

Now _swap_ the two sub-lists:

```
[n - 1] [ 0, 1, ... n - 2 ]
```

Now _join_ them again:

```
[ n - 1, 0, 1, ... n - 2 ]
```

Now, call this list `P` and imagine that it is indexed by the values of `ğ”½ n`. We find that `P[i] â‰¡ +1-mod-n i` for all `i âˆˆ ğ”½ n`.

It turns out that there are a number of functions in modules `Data.Fin` and `Data.Sum` that do something very similar to the split-swap-join exercise we just went through above.

The signatures of these functions are:

```agda
splitAt : âˆ€ m {n} â†’ Fin (m â„•.+ n) â†’ Fin m âŠ Fin n
swap : A âŠ B â†’ B âŠ A
join : âˆ€ m n â†’ Fin m âŠ Fin n â†’ Fin (m â„•.+ n)
```

The behaviour of `join` is worth looking at in more detail.

```agda
join : âˆ€ m n â†’ Fin m âŠ Fin n â†’ Fin (m â„•.+ n)
join m n = [ inject+ n , raise {n} m ]â€²
```

where `[_,_]â€²` is defined as follows:

```agda
[ f , g ]â€² (injâ‚ x) = f x
[ f , g ]â€² (injâ‚‚ y) = g y
```

Simplifying these means that `join` could be rewritten as:

```agda
join m n (injâ‚ x) = inject+ n x
join m n (injâ‚‚ y) = raise {n} m y
```

The type signatures of the two functions on the RHS of the definition above are:

```agda
inject+ : âˆ€ {m} n â†’ Fin m â†’ Fin (m â„•.+ n)
raise : âˆ€ {m} n â†’ Fin m â†’ Fin (n â„•.+ m)
```

Function `inject+` takes a `Fin m` and "injects" it into the `Fin (m â„•.+ n)` type without changing the structure of the value. e.g. For value `suc (suc zero) : Fin 3`, `inject+ (suc (suc zero))` evaluates to `suc (suc zero) : Fin 5` for `m = 3` and `n = 2`.

Function `raise` is a little different to `inject+`. It adds `n` to its argument while increasing the size of the type to `Fin (n â„•.+ m)`. e.g. `raise {3} 2 (suc (suc zero))` evaluates to `suc (suc (suc (suc zero))) : Fin 5`.

We can now now define a function called `splitPermute` that performs the split-swap-join process that I described earlier.

```agda
splitPermute : (m : â„•) {n : â„•} â†’ (ğ”½ (m + n) â†’ ğ”½ (n + m))
splitPermute m {n} = join n m âˆ˜ swap âˆ˜ splitAt m
```

One really nice thing about this function is that for a given `m + n` the inverse of `splitPermute m` is `splitPermute n`.

Remember function `+1-mod-n` from earlier?  It is the special case of `splitPermute` where `n = 1`.

Yet the proof that `splitPermute n` is the left inverse of `splitPermute m` is _much simpler_ than the proof that `-1-mod-n` is the left inverse of `+1-mod-n`.

The make use of the following three theorems from `Data.Fin.Properties` and `Data.Sum.Properties`

```agda
splitAt-join : âˆ€ m n i â†’ splitAt m (join m n i) â‰¡ i
swap-involutive : swap-involutive : swap {A = A} {B = B} âˆ˜ swap â‰— id
join-splitAt : join-splitAt : âˆ€ m n i â†’ join m n (splitAt m i) â‰¡ i
```

Here is the proof!

```agda
  inverse : {m n : â„•} â†’ splitPermute n âˆ˜ splitPermute m â‰— id
  inverse {m} {n} =
    begin
      (splitPermute n âˆ˜ splitPermute m)
    â‰¡âŸ¨âŸ©
      (join m n âˆ˜ swap âˆ˜ splitAt n) âˆ˜ (join n m âˆ˜ swap âˆ˜ splitAt m)
    â‰¡âŸ¨âŸ©
      (join m n âˆ˜ swap âˆ˜ splitAt n âˆ˜ join n m âˆ˜ swap âˆ˜ splitAt m)
    â‰ˆâŸ¨ cong-[ join m n âˆ˜ swap ]âˆ˜âŸ¨ splitAt-join n m âŸ©âˆ˜[ swap âˆ˜ splitAt m ] âŸ©
      (join m n âˆ˜ swap âˆ˜ swap âˆ˜ splitAt m)
    â‰ˆâŸ¨ cong-[ join m n ]âˆ˜âŸ¨ swap-involutive âŸ©âˆ˜[ splitAt m ] âŸ©
      (join m n âˆ˜ splitAt m)
    â‰ˆâŸ¨ join-splitAt m n âŸ©
      id
    âˆ
    where
      open import Relation.Binary.PropositionalEquality
      open import Relation.Binary.Reasoning.Setoid (ğ”½ (m + n) â†’-setoid ğ”½ (m + n))
```

If you're confused about the use of `cong-[_]âˆ˜âŸ¨_âŸ©âˆ˜[_]` above. It's just a nice helper function I wrote to help with _setoid_ reasoning on _extensional equality_ (`_â‰—_`). It is defined as follows:

```agda
  cong-[_]âˆ˜âŸ¨_âŸ©âˆ˜[_] :
    {a : Level} {Aâ€² A B Bâ€² : Set a}
    â†’ (h : B â†’ Bâ€²)
    â†’ {f g : A â†’ B}
    â†’ f â‰— g
    â†’ (hâ€² : Aâ€² â†’ A)
    â†’ h âˆ˜ f âˆ˜ hâ€² â‰— h âˆ˜ g âˆ˜ hâ€²
  cong-[_]âˆ˜âŸ¨_âŸ©âˆ˜[_] h {f} {g} fâ‰—g hâ€² = Î» x â†’ cong h (fâ‰—g (hâ€² x))
    where
      open Relation.Binary.PropositionalEquality using (cong)
```

Agda's ability to define mixfix operators using the `_` character really shon here. See how nice the fragment below looks? The parts between the square brackets remain untouched while the law between the angle brackets (`âŸ¨âŸ©`) applies a law, in this case: `swap-involutive`.

 ```agda
     (join m n âˆ˜ swap âˆ˜ swap âˆ˜ splitAt m)
    â‰ˆâŸ¨ cong-[ join m n ]âˆ˜âŸ¨ swap-involutive âŸ©âˆ˜[ splitAt m ] âŸ©
      (join m n âˆ˜ splitAt m)
```

## Conclusion

In this post I showed you how proving a theorem on a more general formulation of a problem often turns out to be easier than doing it for a specific case. In this particular case you may argue that the specific case could have been defined in terms of `splitAt`, `swap` and `join` _and you'd be correct_. However, I simply didn't spot that at the time. It was only by thinking about what the general pattern of permutation was that allowed me to see that these functions even should be used.

One is likely to generalise a program once a specific solution is found, but proving things on general programs is often easier. Given this is true it makes sense to do the work, up-front, of generalising when programming in a language like Agda.
